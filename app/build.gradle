plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    compileSdk 36

    dependenciesInfo {
        includeInApk = false
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
            universalApk true
        }
    }
    defaultConfig {
        applicationId "wtf.mazy.peel"
        resValue "string", "app_name", "Peel"
        minSdkVersion 28
        targetSdkVersion 36
        versionCode project.hasProperty('VERSION_CODE') ? project.property('VERSION_CODE').toInteger() : 1
        versionName project.hasProperty('VERSION_NAME') ? project.property('VERSION_NAME') : "dev"
    }
    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig true
        resValues true
    }
    signingConfigs {
        release {
            def props = new Properties()
            def propsFile = rootProject.file('local.properties')
            if (propsFile.exists()) {
                props.load(new FileInputStream(propsFile))
            }
            storeFile file(props.getProperty('KEYSTORE_FILE', 'release.keystore'))
            storePassword props.getProperty('KEYSTORE_PASS', '')
            keyAlias props.getProperty('ALIAS_NAME', '')
            keyPassword props.getProperty('ALIAS_PASS', '')
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            applicationIdSuffix = ".debug"
            resValue "string", "app_name", "Peel"
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    namespace 'wtf.mazy.peel'
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jsoup:jsoup:1.22.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0'
    implementation 'androidx.webkit:webkit:1.15.0'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation "androidx.fragment:fragment-ktx:1.8.9"
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'androidx.media3:media3-session:1.9.2'
    implementation 'androidx.room:room-runtime:2.8.4'
    implementation 'androidx.room:room-ktx:2.8.4'
    ksp 'androidx.room:room-compiler:2.8.4'
}